; ----------------------------------------------------------------------------
; PL0 built-in assembler functions
; ----------------------------------------------------------------------------



__COMP16:
            ld      a,h
            and     128
            jr      nz,__NEGM1
            bit     7,d
            ret     nz
            ld      a,h
            cp      d
            ret     nz
            ld      a,l
            cp      e
            ret
__NEGM1:
            xor     d
            rla
            ret     c
            ld      a,h
            cp      d
            ret     nz
            ld      a,l
            cp      e
            ret
;
; MUL10
;

__MUL16:
            ld      c,l
            ld      b,h
            ld      hl,0
            ld      a,15
__MUL16A:
            sla     e
            rl      d
            jr      nc,__MUL16B
            add     hl,bc
__MUL16B:
            add     hl,hl
            dec     a
            jr      nz,__MUL16A
            or      d
            ret     p
            add     hl,bc
            ret

__SDIV16:
            ld      a,h
            ld      (__SREM),a
            xor     d
            ld      (__SQUOT),a

            ld      a,d
            or      a
            jp      p,__CHKDE
            sub     a
            sub     e
            ld      e,a
            sbc     a,a
            sub     d
            ld      d,a
__CHKDE:
            ld      a,h
            or      a
            jp      p,__DODIV
            sub     a
            sub     l
            ld      l,a
            sbc     a,a
            sub     h
            ld      h,a
__DODIV:
            call    __UDIV16
            ret     c

            ld      a,(__SQUOT)
            or      a
            jp      p,__DOREM
            sub     a
            sub     l
            ld      l,a
            sbc     a,a
            sub     h
            ld      h,a
__DOREM:
            ld      a,(__SREM)
            or      a
            ret     p
            sub     a
            sub     e
            ld      e,a
            sbc     a,a
            sub     d
            ld      d,a
            ret

__UDIV16:
            ld      a,e
            or      d
            jr      nz,__DIVIDE
            ld      hl,0
            ld      d,h
            ld      e,l
            scf
            ret
__DIVIDE:
            ld      c,l
            ld      a,h
            ld      hl,0
            ld      b,16
            or      a
__DVLOOP:
            rl      c
            rla
            rl      l
            rl      h
            push    hl
            sbc     hl,de
            ccf
            jr      c,__DROP
            ex      (sp),hl
__DROP:
            inc     sp
            inc     sp
            djnz    __DVLOOP

            ex      de,hl
            rl      c
            ld      l,c
            rla
            ld      h,a
            or      a
            ret

__SQUOT:    ds      1
__SREM:     ds      1
__COUNT:    ds      1





__MUL10:
            ex      de,hl
            add     hl,hl
            ld      d,h
            ld      e,l
            add     hl,hl
            add     hl,hl
            add     hl,de
            ex      de,hl
            ret

;
; DIV10
;
__DIV10:
            ld      bc,0D0Ah
            xor     a
            add     hl,hl
            rla
            add     hl,hl
            rla
            add     hl,hl
            rla
__DIV10A:
            add     hl,hl
            rla
            cp      c
            jr c,   __DIV10B
            sub c
            inc l
__DIV10B:
            djnz __DIV10A
            ret
