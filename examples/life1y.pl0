program GameOfLife;

const
  Width    = 30;
  Height   = 20;
  Percent  = 10;

var
  Map: array[0..1599] of Integer;
  This, Next, Cycle, Live: Integer;

function GetCell(X,Y: Integer): Integer;
begin
  GetCell := Map[This+Y*Width+X];
end;

procedure SetCell(X,Y,Z: Integer);
begin
  Map[Next+Y*Width+X] := Z;
end;

procedure Paint;
var
  X,Y: Integer;
begin
  GotoXY(1,1);
  for Y := 0 to Height - 1 do
  begin
    for X := 0 to Width - 1 do
    begin
      if GetCell(X,Y) = 1 then Write('#') else Write(' ');
    end;
    WriteLn;
  end;
  WriteLn;
end;

function Alive(X,Y: Integer): Boolean;
var
  L,R,U,D,C: Integer;
begin
  if X = 0 then L := Width - 1 else L := X-1;
  if Y = 0 then U := Height - 1 else U := Y-1;

  if X = Width-1 then R := 0 else R := X+1;
  if Y = Height-1 then D := 0 else D := Y+1;

  C := GetCell(L,U) + GetCell(X,U) + GetCell(R,U)
     + GetCell(L,Y)                + GetCell(R,Y)
     + GetCell(L,D) + GetCell(X,D) + GetCell(R,D);

  Alive := (GetCell(X,Y) = 1) and (C=2) or (C=3);
end;

procedure Think;
var
  X,Y,Temp: Integer;
begin
  Temp := This; This := Next; Next := Temp;

  Live := 0;

  for Y := 0 to Height - 1 do
    for X := 0 to Width  - 1 do
      if Alive(X,Y) then
      begin
        SetCell(X,Y,1);
        Live := Live + 1;
      end
      else SetCell(X,Y,0);
end;

procedure Setup;
var
  X,Y: Integer;
begin
  This := 0; Next := 800;

  for Y := 0 to Height - 1 do
    for X := 0 to Width-1 do
      if Random % 100 < Percent then
        SetCell(X,Y,1)
      else
        SetCell(X,Y,0);
end;
  
begin
  ClrScr;
  Setup;

  for Cycle := 1 to 800 do
  begin
    Think;
    TextColor(1 + Cycle % 6);
    CursorOff;
    Paint;
    TextColor(7);
    Write('Cycle:', Cycle,'  Alive:', Live, '  ');
    CursorOn;
  end;
end.